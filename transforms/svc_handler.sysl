CodeGenTransform:
  !view GoName(word <: string) -> string:
    word -> (:
      let splitted = if MatchString("^([A-Z]+[a-z]+|[A-Z]+|[a-z]+)$", word) then [word] else FindAllString("[a-z]+|[A-Z][a-z]+|[A-Z]+", word, -1)
      # Common initialisms https://github.com/golang/lint/blob/8f45f776aaf18cebc8d65861cc70c33c60471952/lint.go#L771
      let initialisms = ["ACL", "API", "ASCII", "CPU", "CSS", "DNS", "EOF", "GUID", "HTML", "HTTP", "HTTPS", "ID", "IP", "JSON", "LHS", "QPS", "RAM", "RHS", "RPC", "SLA", "SMTP", "SQL", "SSH", "TCP", "TLS", "TTL", "UDP", "UI", "UID", "UUID", "URI", "URL", "UTF8", "VM", "XML", "XMPP", "XSRF", "XSS"]

      let wordUpper = splitted -> <sequence of string> (w:
        let upper = ToTitle(w)
        out = if upper in initialisms then upper else Title(w)
      )
      out = Join(wordUpper flatten(.out), "")
    )

  !view ParamType(t <: sysl.Type) -> string:
    t -> (:
      let typeName = if t.type ==:
        "primitive" => if t.primitive ==:
          "DECIMAL" => "Double"
          "INT" => "Int64"
          "FLOAT" => "Float64"
          "STRING" => "string"
          "STRING_8" => "string"
          "BOOL" => "Bool"
          "DATE" => "Date"
          "DATETIME" => "Time"
        else "string"
      out = typeName
    )
    
  !view filename(app <: sysl.App) -> string:
    app -> (:
      filename =  "servicehandler.go"
    )
    
  !view makeValueExpr(strValue <: string) -> Expression:
    strValue -> (:
      Expression = strValue -> <Expression>(:
        ValueExpr = strValue
      )
    )

  !view paramDecl(identifier <: string, typeName <: string) -> ParameterDecl:
    identifier -> (:
      Identifier = identifier
      TypeName = typeName
    )

  !view varDecl(name <: string, typeName <: string) -> VarDecl:
    name -> (:
      VarDecl = name -> <VarDecl> (:
        identifier = name
        TypeName = typeName
      )
    )

  !view handleError(condition <: string, errorCode <: string, errorMsg <: string, errorObj <: string) -> StatementList:
    condition -> (:
      Statement = condition -> <Statement> (:
        IfElseStmt = condition -> <IfElseStmt> (:
          Expression = makeValueExpr(condition).Expression
          Block = condition -> <Block> (:
            let interfaceCall = condition -> <Statement> (:
              Statement = condition -> <Statement> (:
                FunctionCall = condition -> <ReturnStmt> (:
                  FunctionName = "s.serviceInterface.SendErrorResponse"
                  FunctionArgs = condition -> <FunctionArgs> (:
                    Expression = makeValueExpr("w").Expression
                    FuncArgsRest = [makeValueExpr(errorCode), makeValueExpr(errorMsg), makeValueExpr(errorObj)]
                  )
                )
              )
            )
            let returnStmt = condition -> <Statement> (:
              Statement = condition -> <Statement> (:
                ReturnStmt = condition -> <ReturnStmt> (:
                  PayLoad = ""
                )
              )
            )
            StatementList = [interfaceCall, returnStmt]
          )
        )
      )
    )

  !view structVarDecl(bodyParams <: BodyParams) -> sequence of StatementList:
    bodyParams -> (param:
      Statement = varDecl(param.paramName, param.paramType)
    )
    
  !view jsonDecode(bodyParams <: BodyParams, packageName <: string) -> sequence of StatementList:
    packageName -> (:
      let decodeErr = "decodeErr"
      let decodeStmts = bodyParams -> <StatementList> (param:
        Statement = param -> <Statement> (:
          DeclareAndAssignStmt = param -> <DeclareAndAssignStmt> (:
            Variables = decodeErr
            Expression = param -> <Expression> (:
              FunctionCall = param -> <FunctionCall> (:
                FunctionName = "decoder.Decode"
                FunctionArgs = makeValueExpr("&" + param.paramName)
              )
            )
          )
        )
      )
      let condition = decodeErr + " != nil"
      let errorCode = "http.StatusBadRequest"
      let errMsg = '"Error reading request body"'

      StatementList = decodeStmts | [handleError(condition, errorCode, errMsg, "&" + decodeErr)]
    )

  !view methodDef(ep <: sysl.Endpoint) -> string:
    ep -> (:
      let terms = ep.value.pathvars -> <out> (:
        out = "{" + .name + "}"
      )
      let pathVars = terms flatten(.out)
      let path = Split(ep.value.path, "/")
      let method = Title(ToLower(ep.value.method))

      let methodPostfix = path -> <string> (p:
        let postfix  = if p in pathVars then Join(ep.value.pathvars flatten(.name), "") else p
        out = Title(ToLower(postfix))
      )

      methodName = GoName(method + Join(methodPostfix flatten(.out), "")).out
    )

  !view epHandlerMethodSpecs(eps <: set of sysl.Endpoints) -> sequence of MethodSpec:
    eps -> (ep:
      MethodName = methodDef(ep).methodName + "Handler"
      Signature = ep -> <Signature> (:
        Parameters = ep -> <Parameters>(:
          ParameterList = ep -> <ParameterList>(:
            ParameterDecl = paramDecl("w", "http.ResponseWriter")
            ParameterDeclC = [ep] -> <ParameterDeclC>(:
              ParameterDecl = paramDecl("r", "*http.Request")
            )
          )
        )
      )
    )

  !view getParamStatements(paramsMap <: ParamsMap, methodName <: string) -> sequence of StatementList:
    paramsMap -> (param:
      Statement = param -> <Statement> (:
        DeclareAndAssignStmt = param -> <DeclareAndAssignStmt> (:
          Variables = GoName(param.paramName).out
          Expression = param -> <Expression> (:
            FunctionCall = param -> <FunctionCall> (:
              FunctionName = methodName
              FunctionArgs = param -> <FunctionArgs> (:
                Expression = makeValueExpr("r").Expression
                FuncArgsRest = [makeValueExpr('"' + param.paramName + '"')]
              )
            )
          )
        )
      )
    )

  !view declareHeaders(variableName <: string, typeName <: string, values <: sequence of string) -> Statement:
    typeName -> (:
      Statement = typeName -> <Statement> (:
        DeclareAndAssignStmt = typeName -> <DeclareAndAssignStmt> (:
          Variables = variableName
          Expression = typeName -> <Expression> (:
            NewSlice = typeName -> <NewSlice> (:
              TypeName = typeName
              let v = values -> <sequence of string> (:
                out = '"' + . + '"'
              )
              SliceValues = Join(v flatten(.out), ',')
            )
          )   
        )
      )
    )

  !view getHeaderMap(ep <: sysl.Endpoint, methodName <: string) -> sequence of StatementList:
    ep -> (:
      let requiredHeaders = ep.value.params where("header" in .attrs.patterns && "required" in .attrs.patterns)
      let optionalHeaders = ep.value.params where("header" in .attrs.patterns && "optional" in .attrs.patterns)
      let required = declareHeaders("required", "string", requiredHeaders flatten(.attrs.name))
      let optional = declareHeaders("optional", "string", optionalHeaders flatten(.attrs.name))

      let headerMapVar = methodName -> <Statement> (:
        Statement = methodName -> <Statement> (:
          DeclareAndAssignStmt = methodName -> <DeclareAndAssignStmt> (:
            Variables = "headerMap"
            Expression = methodName -> <Expression> (:
              Map = methodName -> <Map> (:
                KeyType = "string"
                ValType = "string"
              )
            )
          )
        )
      )

      let headerMapAssign = methodName -> <Statement> (:
        Statement = methodName -> <Statement> (:
          AssignStmt = methodName -> <AssignStmt> (:
            Variables = "headerMap[header]"
            Expression = makeValueExpr("headerValue").Expression
          )
        )
      )
      
      let headerValue = methodName -> <Statement> (:
        Statement = methodName -> <Statement> (:
          DeclareAndAssignStmt = methodName -> <DeclareAndAssignStmt> (:
            Variables = "headerValue"
            Expression = methodName -> <Expression> (:
              FunctionCall = methodName -> <FunctionCall> (:
                FunctionName = "restlib.GetHeaderParam"
                FunctionArgs = methodName -> <FunctionArgs> (:
                  Expression = makeValueExpr("r").Expression
                  FuncArgsRest = [makeValueExpr("header")]
                )
              )
            )
          )
        )
      )

      let validate= methodName -> <Statement> (:
        Statement = methodName -> <Statement> (:
          IfElseStmt = methodName -> <IfElseStmt> (:
            Expression = makeValueExpr('headerValue == ""').Expression
            Block = methodName -> <Block> (:
              let interfaceCall = methodName -> <Statement> (:
                Statement = methodName -> <Statement> (:
                  FunctionCall = methodName -> <ReturnStmt> (:
                    FunctionName = "s.serviceInterface.SendErrorResponse"
                    FunctionArgs = methodName -> <FunctionArgs> (:
                      Expression = makeValueExpr("w").Expression
                      FuncArgsRest = [makeValueExpr("http.StatusBadRequest"), makeValueExpr('header + " header length is zero"'), makeValueExpr("nil")]
                    )
                  )
                )
              )
              let returnStmt = methodName -> <Statement> (:
                Statement = methodName -> <Statement> (:
                  ReturnStmt = methodName -> <ReturnStmt> (:
                    PayLoad = ""
                  )
                )
              )
              StatementList = [interfaceCall, returnStmt]
            )
          )
        )
      )

      let loopRequired = methodName -> <Statement> (:
        Statement = methodName -> <Statement> (:
          ForLoop = methodName -> <ForLoop> (:
            LoopVar = "_, header"
            Variable = "required"
            Block = methodName -> <StatementList> (:
              StatementList = [headerValue, validate, headerMapAssign]
            )
          )
        )
      )

      let loopOptionals = methodName -> <Statement> (:
        Statement = methodName -> <Statement> (:
          ForLoop = methodName -> <ForLoop> (:
            LoopVar = "_, header"
            Variable = "optional"
            Block = methodName -> <StatementList> (:
              StatementList = [headerValue, headerMapAssign]
            )
          )
        )
      )

      StatementList = [required, optional, headerMapVar, loopRequired, loopOptionals]
    )
  
  !view getReqVars(allParams <: AllParams) -> sequence of StatementList:
    allParams -> (:
      let qParamStatements = getParamStatements(allParams.QueryParams, "restlib.GetQueryParam")
      let pathParamStatements = getParamStatements(allParams.PathParams, "restlib.GetURLParam")
      StatementList = qParamStatements | pathParamStatements
    )

  !view getBodyParams(bodyParams <: BodyParams, packageName <: string) -> sequence of StatementList:
    packageName -> (:
      let decoder = jsonDecoder("decoder")
      let structVarDecl = structVarDecl(bodyParams)
      let jsonDecode = jsonDecode(bodyParams, packageName).StatementList

      StatementList = [decoder] | structVarDecl | jsonDecode
    )

  !view getAllParams(ep <: sysl.Endpoint) -> sequence of AllParams:
    ep -> (:
      let queryParams = ep.value.queryvars -> <QueryParams> (param:
        paramType = ParamType(param).out
        paramName = GoName(param.name).out
      )
      let pathParams = ep.value.pathvars -> <PathParams> (param:
        paramType = ParamType(param).out
        paramName = GoName(param.name).out
      )
      let reqHeaderParams = ep.value.params where("header" in .attrs.patterns && "required" in .attrs.patterns) -> <HeaderParams> (param:
        paramType = ParamType(param).out
        paramName = GoName(param.name).out
        isOptional = false
      )
      let optHeaderParams = ep.value.params where("header" in .attrs.patterns && "optional" in .attrs.patterns) -> <HeaderParams> (param:
        paramType = ParamType(param).out
        paramName = GoName(param.name).out
        isOptional = true
      )
      let bodyParams = ep.value.params where("body" in .attrs.patterns) -> <BodyParams> (param:
        paramType = param.type_ref
        paramName = param.name
      )
      QueryParams = queryParams
      PathParams = pathParams
      HeaderParams = reqHeaderParams | optHeaderParams
      BodyParams = bodyParams
      AllParams = queryParams flatten(.paramName) | pathParams flatten(.paramName) | bodyParams flatten(.paramName)
    )

  !view jsonDecoder(decoderName <: string) -> Statement:
    decoderName -> (:
      Statement = decoderName -> <Statement>(:
        DeclareAndAssignStmt = decoderName -> <DeclareAndAssignStmt>(:
          Variables = decoderName
          Expression = decoderName -> <Expression> (:
            FunctionCall = decoderName -> <FunctionCall> (:
              FunctionName = 'json.NewDecoder'
              FunctionArgs =  makeValueExpr("r.Body")
            )
          )
        )
      )
    )
    
  !view verifyAuthHeaders(ep <: sysl.Endpoint) -> sequence of StatementList:
    ep -> (:
      let condition = '!s.serviceInterface.IsAuthorized(r, headerMap["Authorization"])'
      # Following statement causes gosimple error by golangci-lint
      # let condition = 's.serviceInterface.IsAuthorized(r, headerMap["Authorization"]) == false'
      let errorCode = "http.StatusUnauthorized"
      let errMsg = '"Invalid authorization"'

      StatementList = [handleError(condition, errorCode, errMsg, "nil")]
    )

  !view TopLevelMethodDecl(eps <: set of sysl.Endpoints, packageName <: string) -> sequence of TopLevelDecl:
    eps -> (ep:
      let implFuncName = methodDef(ep).methodName
      let funcName = implFuncName + "Handler"
      let allParams = getAllParams(ep)

      Comment = '// ' + funcName + ' ...'
      MethodDecl = ep -> <MethodDecl>(:
        Receiver = ep -> <Receiver>(:
          ReceiverType = "s *ServiceHandler"
        )
        FunctionName = funcName
        Signature = ep -> <Signature> (:
          Parameters = ep -> <Parameters>(:
            ParameterList = ep -> <ParameterList>(:
              ParameterDecl = paramDecl("w", "http.ResponseWriter")
              ParameterDeclC = [ep] -> <ParameterDeclC>(:
                ParameterDecl = paramDecl("r", "*http.Request")
              )
            )
          )
        )

        let implCall = ep -> <Statement> (:
          Statement = ep -> <Statement> (:
            FunctionCall = ep -> <FunctionCall> (:
              FunctionName = "s.serviceInterface." + implFuncName
              FunctionArgs = ep -> <FunctionArgs> (:
                Expression = makeValueExpr("w").Expression
                FuncArgsRest = if ep.value.params where("header" in .attrs.patterns) count > 0 then [makeValueExpr("headerMap"), makeValueExpr(Join(allParams.AllParams, ", "))] else [makeValueExpr(Join(allParams.AllParams, ", "))]
              )
            )
          )
        )

        Block = ep -> <Block>(:
          let commonStmtList = getReqVars(allParams).StatementList
          let headerStmtList = getHeaderMap(ep, "restlib.GetHeaderParam").StatementList
          let stmtList1 = if ep.value.params where("header" in .attrs.patterns) count > 0 then commonStmtList | headerStmtList else commonStmtList
          let stmtList = if ep.value.params where("header" in .attrs.patterns && "required" in .attrs.patterns && .attrs.name == "Authorization") count > 0 then stmtList1 | verifyAuthHeaders(ep).StatementList else stmtList1
          StatementList = if ep.value.method ==:
            "GET" => stmtList | [implCall]
            "POST" => stmtList  | getBodyParams(allParams.BodyParams, packageName).StatementList | [implCall]
            "PUT" => stmtList  | getBodyParams(allParams.BodyParams, packageName).StatementList | [implCall]
            "DELETE" => stmtList  | [implCall]
        )
      )
    )

  !view goFile(app <: sysl.App) -> string:
    app -> (:
      PackageClause = app -> <package> (:
        let pname = if .attrs.package != null then .attrs.package else app.name
        PackageName = ToLower(pname)
      )

      ImportDecl = app -> <ImportDecl>(:
        let spec = ["encoding/json", "net/http"] -> <ImportSpec> (importPath:
          Import = if importPath == "" then true else '"' + importPath + '"'
        )
        let named = app -> <ImportDecl>(:
          NamedImport = app -> <NamedImport>(:
            Name = "restlib"
            Import = '"' + "github.com/anz-bank/syslgen-examples/lib" + '"'
          )
        )
        ImportSpec = spec | [named]
      )

      let serviceImplVarname = "serviceInterface"

      let svcInterface = [.name] -> <TopLevelDecl> (name:
        let title = "Handler"
        Comment = "// Handler interface for "  + name
        Declaration = title -> <Declaration>(:
          InterfaceType = title -> <InterfaceType>(:
            InterfaceName = title
            MethodSpec =  epHandlerMethodSpecs(app.endpoints)
          )
        )
      )
      
      let serviceHandlerStruct = [.name] -> <TopLevelDecl> (name:
        Comment = "// ServiceHandler for " + name + " API"
        Declaration = name -> <Declaration>(:
          StructType = name -> <StructType>(:
            StructName = "ServiceHandler"
            
            let svcHandlerField = name -> <FieldDecl>(:
              identifier = serviceImplVarname
              Type = "ServiceInterface"
            )

            FieldDecl = [svcHandlerField]
          )
        )
      )

      let newServiceHandler = [.name] -> <TopLevelDecl> (name:
        Comment = "// NewServiceHandler for " + name
        FunctionDecl = name -> <FunctionDecl>(:
          FunctionName = "NewServiceHandler"
          Signature = name -> <Signature> (:
            Parameters = name -> <Parameters>(:
              ParameterList = name -> <ParameterList>(:
                ParameterDecl = paramDecl(serviceImplVarname, "ServiceInterface")
              )
            )

            Result = name -> <Result> (:
              TypeName = "*ServiceHandler"
            )
          )
          Block = name -> <Block>(item:
            StatementList = [item] -> <StatementList>(item:
              Statement = item -> <Statement> (:
                ReturnStmt = item -> <ReturnStmt> (:
                  PayLoad = "&ServiceHandler{" + serviceImplVarname + "}"
                )
              )
            )
          )
        )
      )

      Comment = "//\n//    THIS IS AUTOGENERATED BY syslgen \n//\n"
      
      TopLevelDecl = svcInterface | serviceHandlerStruct | newServiceHandler | TopLevelMethodDecl(app.endpoints, pname)
    )
